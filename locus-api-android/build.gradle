// apply plugins
apply plugin: 'com.android.library'
apply plugin: "kotlin-android"

// Android basics
android {
    namespace = "locus.api.android"

    compileSdkVersion Integer.valueOf(PARAM_COMPILE_SDK_VERSION)
    buildToolsVersion ANDROID_BUILD_TOOLS

    // define default parameters
    defaultConfig {
        minSdkVersion Integer.valueOf(PARAM_MIN_SDK_VERSION)
        targetSdkVersion Integer.valueOf(PARAM_TARGET_SDK_VERSION)

        versionCode Integer.valueOf(API_CODE)
        versionName API_VERSION
    }

    // set compile target
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    buildFeatures {
        aidl = true
    }
}

dependencies {
    // Kotlin
    api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:' + KOTLIN_VERSION
    api 'androidx.annotation:annotation:1.9.1'

    // use in case of direct handling over Locus Core project
    debugApi project(':locus-api-core')
    // use in case of publishing to Maven. 'api' needs to be set in artifact as well
    releaseApi 'com.asamm:locus-api-core:' + API_VERSION
}

// Maven publisher
ext {
    PUBLISH_GROUP_ID = 'com.asamm'
    PUBLISH_ARTIFACT_ID = 'locus-api-android'
    PUBLISH_VERSION = API_VERSION
}
apply from: "${rootProject.projectDir}/gradle/publish-mavencentral.gradle"